buildscript {
	// Fallback use internet ones
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven { url 'http://repo.spring.io/plugins-release' }
		maven { url 'http://repo.spring.io/milestone' }
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	project.repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven { url 'http://repo.spring.io/milestone' }
		maven { url 'http://repo.spring.io/snapshot' }
		maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
		maven { url 'https://oss.sonatype.org/content/repositories/releases' }
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
		maven { url 'http://repo.maven.apache.org/maven2' }
	}
	dependencies {
		classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2'
		classpath 'net.ltgt.gradle:gradle-apt-plugin:0.8'
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
	}
}

apply plugin: 'idea'
apply from: 'gradle/branch.gradle'
apply from: 'gradle/java.gradle'
apply from: 'gradle/lombok.gradle'
apply from: 'gradle/sonar.gradle'
apply from: 'gradle/protobuf.gradle'
apply plugin: 'eu.appsatori.fatjar'

group = 'org.apache.hbase'
version = '01.00.00-SNAPSHOT'
description = 'POC on Hbase coprocessors'

dependencies {
	// Hbase dependencies
	[	"org.apache.hbase:hbase-client:${hbase_version}",
		"org.apache.hbase:hbase-common:${hbase_version}",
		"org.apache.hbase:hbase-protocol:${hbase_version}",
		"org.apache.hbase:hbase-server:${hbase_version}",
		"org.apache.hbase:hbase-annotations:${hbase_version}"].each {
		compile(it){
			exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		}
	}

	// Explicitly add Protobuf dependencies
	compile "com.google.protobuf:protobuf-java:${protobuf_version}"

	// Memory measurement utilities
	compile 'com.github.jbellis:jamm:0.3.1'

	// Testing libraries
	testCompile 'org.assertj:assertj-core:3.6.2'
	testCompile 'junit:junit'

	// Hbase test libraries
	testCompile("org.apache.hbase:hbase-testing-util:${hbase_version}"){
		exclude module: 'slf4j-log4j12'
		exclude module: 'log4j'
	}

	// Logging purpose
	testRuntime 'org.slf4j:slf4j-log4j12:1.7.23'
}

test {
	environment 'PATH', "./developer/bin;${System.getenv()['PATH']}"
	environment 'hadoop.log.dir', ''
	environment 'hadoop.tmp.dir', ''

	ignoreFailures true
	testLogging {
		events 'skipped', 'failed'
	}
}

// Upload all artifacts on our Nexus
uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: nexusUrl) {
				authentication(userName: nexusUser, password: nexusPassword)
			}
			snapshotRepository(url: nexusSnapshotUrl) {
				authentication(userName: nexusUser, password: nexusPassword)
			}
			uniqueVersion = false
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.3'
}

