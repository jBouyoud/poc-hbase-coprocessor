buildscript {
	// Fallback use internet ones
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven { url 'http://repo.spring.io/plugins-release' }
		maven { url 'http://repo.spring.io/milestone' }
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	project.repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven { url 'http://repo.spring.io/milestone' }
		maven { url 'http://repo.spring.io/snapshot' }
		maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
		maven { url 'https://oss.sonatype.org/content/repositories/releases' }
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
		maven { url 'http://repo.maven.apache.org/maven2' }
	}
	dependencies {
		classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2'
		classpath 'net.ltgt.gradle:gradle-apt-plugin:0.8'
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
	}
}

apply plugin: 'idea'
apply from: 'gradle/branch.gradle'
apply from: 'gradle/java.gradle'
apply from: 'gradle/lombok.gradle'
apply from: 'gradle/sonar.gradle'
apply from: 'gradle/protobuf.gradle'
apply plugin: 'eu.appsatori.fatjar'

group = 'org.apache.hbase'
version = '1.0.0-SNAPSHOT'
description = 'POC on Hbase coprocessors'

defaultTasks 'clean', 'build', 'fatJar'

dependencies {
	// Hbase dependencies
	["org.apache.hbase:hbase-client:${hbase_version}",
	 "org.apache.hbase:hbase-common:${hbase_version}",
	 "org.apache.hbase:hbase-protocol:${hbase_version}",
	 "org.apache.hbase:hbase-server:${hbase_version}",
	 "org.apache.hbase:hbase-annotations:${hbase_version}"].each {
		compile(it) {
			exclude group: 'org.slf4j', module: 'slf4j-log4j12'
			ext {
				fatJarExclude = true
			}
		}
	}

	// Copy of hbase-protocol files to compile, extract *.proto and avoid compiling them twice
	// testCompile files('lib/hbase-protocol-1.2.3-protobuf.jar') FIXME: Runtime issue due to packages

	// Explicitly add Protobuf dependencies
	compile("com.google.protobuf:protobuf-java:${protobuf_version}") {
		ext {
			fatJarExclude = true
		}
	}

	// Bytecode modifiction
	compile "org.javassist:javassist:${javassist_version}"

	// Testing libraries
	testCompile 'org.assertj:assertj-core:3.6.2'
	testCompile 'junit:junit'

	// Hbase test libraries
	testCompile("org.apache.hbase:hbase-testing-util:${hbase_version}") {
		exclude module: 'slf4j-log4j12'
		exclude module: 'log4j'
	}

	// Java agent utilities (able to load agent on JVM at runtime)
	testCompile 'com.ea.agentloader:ea-agent-loader:1.0.2'

	// Logging purpose
	testRuntime 'org.slf4j:slf4j-log4j12:1.7.23'
}

tasks.withType(Jar) {
	manifest {
		attributes(
			'Premain-Class': 'fr.poc.hbase.coprocessor.policy.agent.CoprocessorPolicyAgent',
			'Agent-Class': 'fr.poc.hbase.coprocessor.policy.agent.CoprocessorPolicyAgent',
			'Can-Redefine-Classes': 'true',
			'Can-Retransform-Classes': 'true',
			'Can-Set-Native-Method-Prefix': 'false'
		)
	}
}

test {
	environment 'PATH', "./developer/bin;${System.getenv()['PATH']}"
	environment 'hadoop.log.dir', ''
	environment 'hadoop.tmp.dir', ''

	exclude '**/*AgentTest*'
	ignoreFailures true
	testLogging {
		events 'skipped', 'failed'
	}
}


task agentTest(type: Test, description: 'Runs the tests with policy agent', group: 'verification') {
	testClassesDir = sourceSets.test.output.classesDir
	classpath = sourceSets.test.runtimeClasspath

	environment 'PATH', "./developer/bin;${System.getenv()['PATH']}"
	environment 'hadoop.log.dir', ''
	environment 'hadoop.tmp.dir', ''

	include '**/*AgentTest*'
	ignoreFailures true
	testLogging {
		events 'skipped', 'failed'
	}
}
test.dependsOn agentTest

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}

fatJar{
	exclude 'META-INF/*.MF'
}
